'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os
import subprocess

import buildutils.userspace

Import(['userspace_env'])

env = userspace_env

builddir = env["PEDIGREE_BUILD_BASE"]

NEWLIB_VERSION = '1.17.0'
NEWLIB_BASENAME = 'newlib-%s' % (NEWLIB_VERSION,)
NEWLIB_FILENAME = '%s.tar.gz' % (NEWLIB_BASENAME,)
NEWLIB_CONFIGURE = os.path.join(NEWLIB_BASENAME, 'newlib', 'configure')

NEWLIB_OUTPUT_LIBC = env.File(os.path.join(builddir, 'stock-libg.a'))
NEWLIB_OUTPUT_LIBM = env.File(os.path.join(builddir, 'stock-libm.a'))

NEWLIB_PATCH = '#compilers/pedigree-newlib.patch'
NEWLIB_BUILDER = '#scripts/build-newlib.sh'

downloaded = env.Download(
    NEWLIB_FILENAME,
    'ftp://sourceware.org/pub/newlib/%s' % (NEWLIB_FILENAME,))

env.ExtractAndPatchTar(NEWLIB_BASENAME, [NEWLIB_FILENAME, NEWLIB_PATCH])

def buildNewlib(target, source, env):
    ccflags = buildutils.userspace.fixFlags(env, env['CCFLAGS'])
    cflags = buildutils.userspace.fixFlags(env, env['CFLAGS'])

    # Pass some information down to the child.
    childenv = {}
    childenv['COMPILER_TARGET'] = env['COMPILER_TARGET']
    childenv['XGCC'] = env['CC']
    childenv['XLD'] = env['LINK']
    childenv['XCFLAGS'] = cflags + ccflags
    childenv['SRCDIR'] = env.Dir('#').abspath
    childenv['DROPDIR'] = target[0].dir.abspath

    # Remove -Werror if it exists in CFLAGS for the newlib build.
    try:
        childenv['XCFLAGS'].remove('-Werror')
    except ValueError:
        pass  # Not found.

    # Convert to string for passing through subprocess module.
    childenv['XCFLAGS'] = ' '.join(childenv['XCFLAGS'])

    subprocess.check_call([source[0].abspath], cwd=source[1].abspath, env=childenv)

output_libs = env.Command(
    [NEWLIB_OUTPUT_LIBC, NEWLIB_OUTPUT_LIBM],
    [NEWLIB_BUILDER, NEWLIB_BASENAME],
    Action(buildNewlib, env.get('CCCOMSTR')))

# Need POSIX headers to be installed before we can build Newlib.
env.Requires(output_libs, 'headers')
