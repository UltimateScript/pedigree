'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os
import SCons

import buildutils.misc

Import('env')

# These dirs are expected to have source files (C, C++) that can be used with
# xgettext; the form is (domain, path).
translate_dirs = [
    ('live', '#src/user/applications/live'),
    ('login', '#src/user/applications/login'),
]

for domain, path in translate_dirs:
    files = Glob(os.path.join(path, '*.c')) + Glob(os.path.join(path, '*.cc'))
    if not files:
        continue

    languages = buildutils.misc.getLanguages(env)

    # Collect .po files and update them in the source directory.
    po_targets = [env.Dir('%s/%s' % (lang, domain)).srcnode().abspath
                  for lang in languages]
    # Collect expected .mo files and place them in the build directory.
    mo_targets = ['locale/%s/LC_MESSAGES/%s' % (lang, domain)
                  for lang in languages]

    env.POTUpdate(domain, files)
    env.POUpdate(po_targets, POTDOMAIN=domain)
    for po, mo in zip(po_targets, mo_targets):
        r = env.MOFiles(mo, po)

        env.Alias('i18n', r)
