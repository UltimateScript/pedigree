'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os
import subprocess

import buildutils.userspace

Import(['userspace_env'])

env = userspace_env

builddir = env["PEDIGREE_BUILD_BASE"]

MUSL_VERSION = '1.1.14'
MUSL_BASENAME = 'musl-%s' % (MUSL_VERSION,)
MUSL_FILENAME = '%s.tar.gz' % (MUSL_BASENAME,)
MUSL_CONFIGURE = os.path.join(MUSL_BASENAME, 'configure')

MUSL_BUILDER = '#scripts/build-musl.sh'

downloaded = env.Download(
    MUSL_FILENAME, 'https://www.musl-libc.org/releases/%s' % MUSL_FILENAME)

MUSL_OUTPUT_DIRECTORY = os.path.join(builddir, 'musl')
MUSL_LIB_DIRECTORY = os.path.join(builddir, 'musl', 'lib')
MUSL_INCLUDE_DIRECTORY = os.path.join(builddir, 'musl', 'include')

env.Extract(MUSL_BASENAME, MUSL_FILENAME)

def buildMusl(target, source, env):
    flags = buildutils.userspace.fixFlags(env, env['CCFLAGS'] + env['CFLAGS'])

    new_env = {
        'CC': env['CC'],
        'LD': env['LINK'],
        'COMPILER_TARGET': env['COMPILER_TARGET'],
        'SRCDIR': env.Dir('#').abspath,
        'TARGETDIR': os.path.join(target[0].dir.abspath, '..'),
        'ARCH_TARGET': env['ARCH_TARGET'],
    }

    subprocess.check_call([source[0].abspath], cwd=source[1].abspath, env=new_env)

output_libs = env.Command(
    [
        os.path.join(MUSL_LIB_DIRECTORY, 'libc.so'),
        os.path.join(MUSL_LIB_DIRECTORY, 'ld-musl-x86_64.so.1'),
        os.path.join(MUSL_LIB_DIRECTORY, 'crt1.o'),
        os.path.join(MUSL_LIB_DIRECTORY, 'crti.o'),
        os.path.join(MUSL_LIB_DIRECTORY, 'crtn.o'),
        os.path.join(MUSL_LIB_DIRECTORY, 'rcrt1.o'),
        os.path.join(MUSL_LIB_DIRECTORY, 'Scrt1.o'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libc.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libcrypt.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libdl.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libm.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'librt.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libpthread.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libresolv.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libutil.a'),
        os.path.join(MUSL_LIB_DIRECTORY, 'libxnet.a'),
    ],
    # Also depend on our drop-in files.
    [MUSL_BUILDER, MUSL_BASENAME] + Glob('*.h') + Glob('*.c') + Glob('*.musl-s'),
    Action(buildMusl, env.get('CCCOMSTR')))

env.Alias('crt', output_libs)
env.Alias('pthreads', output_libs)
env.Alias('headers', output_libs)
env.Alias('libs', output_libs)
