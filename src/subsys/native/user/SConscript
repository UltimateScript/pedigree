'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace

Import(['userspace_env'])

env = userspace_env

env_clone = env.Clone()

# Build directories
builddir = env["PEDIGREE_BUILD_BASE"]
kerneldir = env["PEDIGREE_BUILD_KERNEL"]
moduledir = env["PEDIGREE_BUILD_MODULES"]
subsysdir = env["PEDIGREE_BUILD_SUBSYS"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]

baseInclude = env.Dir("../include").srcnode()

# We output a single library, which applications link against to gain access to
# the implementation of the native subsystem classes.
native_userlib_objname = builddir.File('libpedigree.a')
native_userlib_shobjname = builddir.File('libpedigree.so')

env.Alias("libs", native_userlib_objname)
env.Alias("libs", native_userlib_shobjname)
env.Depends(native_userlib_shobjname, "crt")

# XXX: We have an unfortunate dependency on libc.
if env['posix_musl']:
    libpath = builddir.Dir('musl').Dir('lib')
    libc_objname = libpath.File('libc.so')
else:
    libpath = builddir
    libc_objname = libpath.File('libc.so')
env.Depends(native_userlib_shobjname, libc_objname)

# We also depend on pedigree-c
env.Depends(native_userlib_shobjname, builddir.File('libpedigree-c.so'))

files = [
    Glob("*.cc"),
    Glob("config/*.cc"),
    Glob("graphics/*.cc"),
    Glob("input/*.cc"),
    Glob("ipc/*.cc")
]

include_dirs = [
    '#src',
    '#src/subsys',
    builddir.Dir('musl').Dir('include'),
    '#images/local/include',
    '#images/local/include/native',
    '#src/system/include',
    '.',
]

if not env['ON_PEDIGREE']:
    cpp_base = '#images/local/support/gcc/include/c++/%s' % env['CCVERSION']
    include_dirs.extend([
        cpp_base,
        '%s/%s' % (cpp_base, env['COMPILER_TARGET']),
    ])

env_clone.MergeFlags({
    'LIBPATH': [libpath, builddir],
    'CFLAGS': ['-D__STDC_VERSION__=201112L'],
    'CXXFLAGS': ['-D__STDC_VERSION__=0L'],
})

env_clone['CPPPATH'] = include_dirs
env_clone['LIBS'] = ['pedigree-c', 'gcc']

# -fPIC for libpedigree
glueFlags = ['-fPIC', '-DUSE_PIC_SYSCALLS']
env_clone.MergeFlags(glueFlags)

env_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CFLAGS'])
env_clone['CCFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CCFLAGS'])
env_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CXXFLAGS'])

env_clone.Library(native_userlib_objname, files)
env_clone.SharedLibrary(native_userlib_shobjname, files, LINKFLAGS='')

# Install native subsystem headers to the local images directory.
buildutils.fs.install_tree(imagesdir.Dir('include'), baseInclude, env, alias='native-headers')
